// SPDX-License-Identifier: MIT
// https://docs.quadrans.io/programming/solidity_101.html
//pragma solidity ^0.8.0;
pragma solidity >=0.4.22 <0.9.0;

import "./ERC721/ERC721.sol";
import "./access/AccessControlEnumerable.sol";
import "./ERC721/extensions/ERC721Enumerable.sol";
import "./ERC721/extensions/ERC721Burnable.sol";
import "./ERC721/extensions/ERC721Pausable.sol";
import "./utils/Counters.sol";
import "./utils/cryptography/ECDSA.sol";
import "./utils/math/SafeMath.sol";
import "./access/Ownable.sol";

/**
 * @dev Valorem Real Estate NFT
 * @author Burt Snyder / Open Zeppelin
 * Facilitate buying a percentage of real estate as an nft token.
 * - Tokens updated on appraisel
 * - ...
 */
contract VContract is
    Context,
    AccessControlEnumerable,
    ERC721Enumerable,
    ERC721Burnable,
    ERC721Pausable,
    Ownable
{
    using Counters for Counters.Counter;
    using ECDSA for bytes32;
    using SafeMath for uint256;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");
    string private _baseTokenURI;

    Counters.Counter private _tokenIdTracker;

    //last token minted
    uint256 private _tokenId;

    //dev mode testing
    bool private _devMode = true;

    /**
     * @dev Property details update purchaseAmount on transfer
     */
    struct Property {
        string name;
        uint256 currentValue;
        uint256 usedValue;
    }

    /**
     * @dev Percentage of property owned
     */
    struct Percentage {
        uint256 percentage;
    }

    struct TokenSaleState {
        uint256 tokenId;
        address tokenOwner;
        uint256 salePrice;
        bool forSale;
    }

    TokenSaleState[] private tknSaleState;
    Property[] private properties;
    Percentage[] private percentages;

    //map property to tokenId
    mapping(uint256 => uint256) public propertyToToken;

    //map percentage to tokenId
    mapping(uint256 => uint256) public percentageToToken;

    //prevent re-runs
    mapping(address => mapping(uint256 => bool)) usedNonces;

    //tokens for sale
    mapping(uint256 => TokenSaleState) public tokenToSaleState;

    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, `MINTER_ROLE` and `PAUSER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    constructor(
        string memory name,
        string memory symbol,
        string memory baseTokenURI
    ) ERC721(name, symbol) {
        _baseTokenURI = baseTokenURI;
        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
        _setupRole(MINTER_ROLE, _msgSender());
        _setupRole(PAUSER_ROLE, _msgSender());
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    /**
     * @dev Checks mapping for property name
     * that is established on mint. Returns property name based
     * on token id.
     */
    function propertyName(uint256 tokenId)
        public
        view
        virtual
        returns (string memory)
    {
        uint256 _id = propertyToToken[tokenId];
        return properties[_id].name;
    }

    /**
     * @dev returns baseTokenUri
     */
    function getBaseTokenURI() external view returns (string memory) {
        return _baseTokenURI;
    }

    /**
     * @dev returns baseTokenUri
     */
    function getPropertiesSize() external view returns (uint256 count) {
        return properties.length;
    }

    /**
     * @dev returns property
     */
    function getProperty(uint256 tokenId)
        external
        view
        returns (
            string memory name,
            uint256 currentValue,
            uint256 usedValue
        )
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return (p.name, p.currentValue, p.usedValue);
    }

    /**
     * @dev returns property currentValue
     */
    function getPropertyCurrentValue(uint256 tokenId)
        external
        view
        returns (uint256 currentValue)
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return p.currentValue;
    }

    /**
     * @dev returns property usedValue (1-100 %)
     */
    function getPropertyUsedValue(uint256 tokenId)
        external
        view
        returns (uint256 usedValue)
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return p.usedValue;
    }

    /**
     * @dev returns property URI
     */
    function getPropertyName(uint256 tokenId)
        external
        view
        returns (string memory name)
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return p.name;
    }

    /**
     * @dev update _baseTokenURI
     */
    function updateBaseTokenURI(
        string memory _uri,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) external {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Update Property Uri"
        );
        _baseTokenURI = _uri;
    }

    /**
     * @dev private add new property uri to mapping set on mint
     */
    function _setPropertyOnMint(
        uint256 tokenId,
        string memory _name,
        uint256 _currentValue,
        uint256 _percentage
    ) internal {
        properties.push(Property(_name, _currentValue, _percentage));
        uint256 id = properties.length - 1;
        propertyToToken[tokenId] = id;
    }

    /**
     * @dev private add new property uri to mapping set on mint
     */
    function _setExistingPropertyOnMint(
        uint256 tokenId,
        uint256 propertyIndex,
        uint256 percentage
    ) internal returns (bool) {
        Property storage _property = properties[propertyIndex];
        uint256 result = _property.usedValue.add(percentage);
        if (result <= 10000) {
            _property.usedValue = result;
            properties[propertyIndex] = _property;
            propertyToToken[tokenId] = propertyIndex;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev set percentage value of property
     */
    function getPercentageValueOfProperty(uint256 tokenId)
        external
        view
        returns (uint256 percentage)
    {
        uint256 _propertyId = propertyToToken[tokenId];
        Property storage _property = properties[_propertyId];
        uint256 _currentValue = _property.currentValue;

        uint256 _percentageId = percentageToToken[tokenId];
        Percentage storage _p = percentages[_percentageId];
        uint256 _percentage = _p.percentage;

        uint256 basispoint = _percentage;
        return calculatePropertyPercentage(_currentValue, basispoint);
    }

    /**
     * @dev update property currentValue on appraisal
     */
    function updatePropertyCurrentValue(
        uint256 tokenId,
        uint256 _currentValue,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) external {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Update Property Value"
        );
        uint256 _id = propertyToToken[tokenId];
        Property storage _property = properties[_id];
        _property.currentValue = _currentValue;
        properties[_id] = _property;
    }

    /**
     * @dev update property uri to mapping by minter
     * TODO: probably not a good idea at this point...
     */
    function updatePropertyName(
        uint256 tokenId,
        string memory _name,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) external {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Update Property Name"
        );
        uint256 _id = propertyToToken[tokenId];
        Property storage _property = properties[_id];
        _property.name = _name;
        properties[_id] = _property;
    }

    /**
     * @dev private add new property uri to mapping set on mint
     */
    function _setPercentageOnMint(uint256 tokenId, uint256 _percentage)
        internal
    {
        percentages.push(Percentage(_percentage));
        uint256 id = percentages.length - 1;
        percentageToToken[tokenId] = id;
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must be authorized to mint via _isOwner
     *
     * Extra:
     * - set last token minted: _tokenId
     * - set property uri address if you want or value to add to base
     */
    function mint(
        address to,
        bool newProperty, // new or existing property?
        uint256 propertyIndex, // if existing what index?
        string memory _propertyName,
        uint256 _currentValue, //ethers.utils.parseUnits(n, 2)
        uint256 _percentage,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) public virtual {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Mint"
        );
        _mint(to, _tokenIdTracker.current());
        _tokenId = _tokenIdTracker.current();

        if (newProperty) {
            _setPropertyOnMint(
                _tokenIdTracker.current(),
                _propertyName,
                _currentValue,
                _percentage
            );
        } else {
            require(
                _setExistingPropertyOnMint(
                    _tokenIdTracker.current(),
                    propertyIndex,
                    _percentage
                ),
                "VContract: Property % Exceeds Availablity"
            );
            Property storage _property = properties[propertyIndex];
            _currentValue = _property.currentValue;
        }
        _setPercentageOnMint(_tokenIdTracker.current(), _percentage);

        _tokenIdTracker.increment();
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721, ERC721Enumerable, ERC721Pausable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }

    // added for relay services i.e. itx infura
    // _msgSender not same set in constructor for
    // future transactions
    function _isOwner(
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) internal returns (bool) {
        if (_devMode) return true;
        require(!usedNonces[owner()][nonce], "Vcontract: Bad Nonce");
        address signer = ECDSA.recover(message, signature);
        if (signer == owner()) {
            usedNonces[signer][nonce] = true;
            return true;
        } else {
            return false;
        }
    }

    function getOwner() external view returns (address) {
        return owner();
    }

    //https://www.omnicalculator.com/finance/basis-point
    //https://www.investopedia.com/ask/answers/what-basis-point-bps/
    // find basis point from percentage by > percentage * 100
    // .i.e 0.5 * 100 = 50 bps
    function calculatePropertyPercentage(uint256 amount, uint256 basispoint)
        internal
        pure
        returns (uint256)
    {
        //theNumber is never a decimal
        return amount.mul(basispoint).div(10000); //returns bps add .00
    }

    /**
     * @dev
     * returns the last id of minted token
     */
    function lastTokenMinted() public view returns (uint256) {
        // return BN { negative: 0, words: [ 3, <1 empty item> ], length: 1, red: null }
        return _tokenId;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override(AccessControlEnumerable, ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }

    function enableTokenSale(
        uint256 tokenId,
        uint256 salePrice,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) external {
        require(
            !usedNonces[ownerOf(tokenId)][nonce],
            "Vcontract: Error Bad Nonce"
        );
        address signer = ECDSA.recover(message, signature);
        require(
            ownerOf(tokenId) == signer,
            "VContract: You Don't Own This Token"
        );
        usedNonces[signer][nonce] = true;
        tknSaleState.push(TokenSaleState(tokenId, signer, salePrice, true));
        emit Approval(signer, address(this), tokenId);
    }

    function buy(uint256 tokenId) external payable {
        // 3 % of Sale Price To Contract Owner
        address payable contractOwner = payable(
            0x5D591d99547FB4b643559675f78487547454C4B8
        );
        address buyer = msg.sender;
        uint256 paid = msg.value;
        require(
            tokenToSaleState[tokenId].forSale,
            "VContract: Token Not For Sale"
        );
        require(
            getApproved(tokenId) == address(this),
            "VContract: Token Id Not Approved"
        );
        require(
            tokenToSaleState[tokenId].salePrice >= paid,
            "VContract: Amount Error, Check Sale Price"
        );
        contractOwner.transfer(msg.value / 300);
        delete tokenToSaleState[tokenId];
        //transferFrom(ownerOf(tokenId), buyer, tokenId);
        emit Transfer(ownerOf(tokenId), buyer, tokenId);
    }
}
