// SPDX-License-Identifier: MIT
// https://docs.quadrans.io/programming/solidity_101.html
//pragma solidity ^0.8.0;
pragma solidity >=0.4.22 <0.9.0;

import "./ERC721/ERC721.sol";
import "./access/AccessControlEnumerable.sol";
import "./ERC721/extensions/ERC721Enumerable.sol";
import "./ERC721/extensions/ERC721Burnable.sol";
import "./ERC721/extensions/ERC721Pausable.sol";
import "./utils/Counters.sol";
import "./utils/cryptography/ECDSA.sol";
import "./utils/math/SafeMath.sol";
import "./access/Ownable.sol";

/**
 * @dev Valorem Real Estate NFT
 * @author Burt Snyder / Open Zeppelin
 * Facilitate buying a percentage of real estate as an nft token.
 * - Tokens updated on appraisel
 * - ...
 */
contract VContract is
    Context,
    AccessControlEnumerable,
    ERC721Enumerable,
    ERC721Burnable,
    ERC721Pausable,
    Ownable
{
    using Counters for Counters.Counter;
    using ECDSA for bytes32;
    using SafeMath for uint256;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");
    string private _baseTokenURI;

    Counters.Counter private _tokenIdTracker;

    //last token minted
    uint256 private _tokenId;

    //dev mode testing
    bool private _devMode = true;

    //test number
    uint256 private _testNumber;

    /**
     * @dev Property details update purchaseAmount on transfer
     */
    struct Property {
        string URI;
        uint256 currentValue;
        uint256 usedValue;
    }

    /**
     * @dev Percentage of property owned
     */
    struct Percentage {
        uint256 percentage;
    }

    Property[] private properties;
    Percentage[] private percentages;

    //map property to tokenId
    mapping(uint256 => uint256) public propertyToToken;

    //map percentage to tokenId
    mapping(uint256 => uint256) public percentageToToken;

    //prevent re-runs
    mapping(address => mapping(uint256 => bool)) usedNonces;

    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, `MINTER_ROLE` and `PAUSER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    constructor(
        string memory name,
        string memory symbol,
        string memory baseTokenURI
    ) ERC721(name, symbol) {
        _baseTokenURI = baseTokenURI;
        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
        _setupRole(MINTER_ROLE, _msgSender());
        _setupRole(PAUSER_ROLE, _msgSender());
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    function setTestNumber(uint256 bn) external {
        _testNumber = bn;
    }

    function getTestNumber() public view returns (uint256) {
        return _testNumber;
    }

    // function testCurrency() public pure returns (uint256) {
    //     uint256 amount = 10 * 10**18;
    //     uint256 price = 0.5 * 10**18;
    //     return amount / price;
    // }

    /**
     * @dev Checks mapping for property uri
     * that is established on mint. Returns property uri based
     * on token id.
     */
    function propertyURI(uint256 tokenId)
        public
        view
        virtual
        returns (string memory)
    {
        uint256 _id = propertyToToken[tokenId];
        return properties[_id].URI;
    }

    /**
     * @dev
     * returns the last id of minted token
     */
    function lastTokenMinted() public view returns (uint256) {
        // return BN { negative: 0, words: [ 3, <1 empty item> ], length: 1, red: null }
        return _tokenId;
    }

    /**
     * @dev returns baseTokenUri
     */
    function getBaseTokenURI() external view returns (string memory) {
        return _baseTokenURI;
    }

    /**
     * @dev returns baseTokenUri
     */
    function getPropertiesSize() external view returns (uint256 count) {
        return properties.length;
    }

    /**
     * @dev returns property
     */
    function getProperty(uint256 tokenId)
        external
        view
        returns (string memory URI, uint256 currentValue)
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return (p.URI, p.currentValue);
    }

    /**
     * @dev returns property
     */
    function getPropertyCurrentValue(uint256 tokenId)
        external
        view
        returns (uint256 currentValue)
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return p.currentValue;
    }

    /**
     * @dev returns property
     */
    function getPropertyURI(uint256 tokenId)
        external
        view
        returns (string memory URI)
    {
        uint256 _id = propertyToToken[tokenId];
        Property memory p = properties[_id];
        return p.URI;
    }

    /**
     * @dev returns percentage of property owned
     */
    function getPercentage(uint256 tokenId)
        external
        view
        returns (uint256 _percentage)
    {
        uint256 _id = percentageToToken[tokenId];
        Percentage memory p = percentages[_id];
        return (p.percentage);
    }

    /**
     * @dev update _baseTokenURI
     */
    function updateBaseTokenURI(
        string memory _uri,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) external {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Update Property Uri"
        );
        _baseTokenURI = _uri;
    }

    /**
     * @dev private add new property uri to mapping set on mint
     */
    function _setPropertyOnMint(
        uint256 tokenId,
        string memory _uri,
        uint256 _currentValue,
        uint256 _percentage
    ) internal {
        properties.push(Property(_uri, _currentValue, _percentage));
        uint256 id = properties.length - 1;
        propertyToToken[tokenId] = id;
    }

    /**
     * @dev private add new property uri to mapping set on mint
     */
    function _setExistingPropertyOnMint(
        uint256 tokenId,
        uint256 propertyIndex,
        uint256 percentage
    ) internal returns (bool) {
        Property storage _property = properties[propertyIndex];
        uint256 result = _property.usedValue.add(percentage);
        if (result <= 100) {
            _property.usedValue = result;
            properties[propertyIndex] = _property;
            propertyToToken[tokenId] = propertyIndex;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev private add new property uri to mapping set on mint
     */
    function _setPercentageOnMint(uint256 tokenId, uint256 _percentage)
        internal
    {
        percentages.push(Percentage(_percentage));
        uint256 id = percentages.length - 1;
        percentageToToken[tokenId] = id;
    }

    /**
     * @dev update property uri to mapping by minter
     */
    function updatePropertyURI(
        uint256 tokenId,
        string memory _uri,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) external {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Update Property Uri"
        );
        uint256 _id = propertyToToken[tokenId];
        Property storage _property = properties[_id];
        _property.URI = _uri;
        properties[_id] = _property;
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must be authorized to mint via _isOwner
     *
     * Extra:
     * - set last token minted: _tokenId
     * - set property uri address if you want or value to add to base
     */
    function mint(
        address to,
        bool newProperty, // new or existing property?
        uint256 propertyIndex, // if existing what index?
        string memory _propertyURI,
        uint256 _currentValue, //ethers.utils.parseUnits(n, 2)
        uint256 _percentage,
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) public virtual {
        require(
            _isOwner(message, nonce, signature),
            "VContract: Not Authorized to Mint"
        );
        _mint(to, _tokenIdTracker.current());
        _tokenId = _tokenIdTracker.current();

        if (bytes(_propertyURI).length < 1) {
            _propertyURI = string(abi.encodePacked(_tokenIdTracker.current()));
            //_propertyURI = string(abi.encodePacked(_tokenId));
        }

        if (newProperty) {
            _setPropertyOnMint(
                _tokenIdTracker.current(),
                _propertyURI,
                _currentValue,
                _percentage
            );
        } else {
            require(
                _setExistingPropertyOnMint(
                    _tokenIdTracker.current(),
                    propertyIndex,
                    _percentage
                ),
                "VContract: Percentage Exceeds Availablity"
            );
        }
        _setPercentageOnMint(
            _tokenIdTracker.current(),
            mulScale(_currentValue, _percentage, 100)
        );

        _tokenIdTracker.increment();
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721, ERC721Enumerable, ERC721Pausable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }

    // added for relay services i.e. itx infura
    // _msgSender not same set in constructor for
    // future transactions
    function _isOwner(
        bytes32 message,
        uint256 nonce,
        bytes memory signature
    ) internal returns (bool) {
        if (_devMode) return true;
        require(!usedNonces[owner()][nonce], "Vcontract: Bad Nonce");
        address signer = ECDSA.recover(message, signature);
        if (signer == owner()) {
            usedNonces[signer][nonce] = true;
            return true;
        } else {
            return false;
        }
    }

    // Calculate x * y / scale rounding down.
    // https://ethereum.stackexchange.com/questions/55701/how-to-do-solidity-percentage-calculation
    function mulScale(
        uint256 x,
        uint256 y,
        uint128 scale
    ) internal pure returns (uint256) {
        uint256 a = x / scale;
        uint256 b = x % scale;
        uint256 c = y / scale;
        uint256 d = y % scale;

        return a * c * scale + a * d + b * c + (b * d) / scale;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override(AccessControlEnumerable, ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
}
